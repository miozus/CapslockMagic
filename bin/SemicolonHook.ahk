SendMode "Input"
SetWorkingDir A_ScriptDir

;=====================================================================o
;                      Semicolon Hook Registry

global EnableSemicolonMonkey := false
; all dict comes from database
semicolonHookStr := getKeyStr(secretDictionary) "," getKeyStr(userDictionary)
semicolonHook := InputHook("C", "{Space}{Esc}", semicolonHookStr)
semicolonHook.OnChar := onTypoChar
semicolonHook.OnEnd := onTypoEnd

+`;:: Send "{blind}:"
*`;::
{
    global semicolonHook, EnableSemicolonMonkey
    thisHotkey := A_ThisHotkey
    GC.disableOtherHotkey(thisHotkey)
    EnableSemicolonMonkey := true
    KeyWait ";"
    EnableSemicolonMonkey := false
    if (A_PriorKey == ";" && A_TimeSinceThisHotkey < 350)
    {
        enterSemicolonAbbr(semicolonHook)
    }
    GC.enableOtherHotkey(thisHotkey)
}

; è‡ªå®šä¹‰è¾“å…¥æ³•
; ä¼˜å…ˆ: æ— å‘½ä»¤è¯å…¸ï¼ˆçº¯æ–‡å­—ï¼‰ï¼›ä¼˜å…ˆæ‰“å°è‡ªå®šä¹‰çŸ­è¯­ï¼Œæ”¯æŒè·¨å¹³å°å¯¼å‡ºï¼Œè¾“å…¥æ³•é€šç”¨
; å…¶æ¬¡: è‡ªå®šä¹‰è„šæœ¬åŠŸèƒ½
;
; å®˜æ–¹æ–‡æ¡£è¯´ï¼Œswitch å‘½ä¸­å°±è¿”å›ï¼Œæ²¡æœ‰è½ä¸‹ç›´é€šï¼Œæ‰€ä»¥ä¸ç”¨ return
; ä½†ä¸€ç§è§‚ç‚¹è®¤ä¸ºï¼Œå‘½ä¸­åè¿”å›ï¼Œå¯ä»¥æå‰ç»“æŸ hook ç›‘å¬è¾“å…¥ï¼Œé˜²æ­¢å‚ç›´è½ä¸‹ã€‚
enterSemicolonAbbr(ih) {
    ToolTip "ğŸ™ˆ"
    ih.start()
    ih.wait()
    ih.stop()
    if (ih.Match)
    {
        try {
            value := "{text}" userDictionary[ih.Match]
            Send value
            moveCursorLeftIfEndWithBracket(value)
            return
        }
        ; å¸¦å‘½ä»¤è¯å…¸: åŠŸèƒ½æ¯”è¾ƒé‡
        ToolTip "ğŸ™‰ " secretDictionary[ih.Match]
        execSemicolonAbbr(ih.Match)
    } else {
        ; æœªæ”¶å½•è¯å…¸ï¼ŒçŒ´å­è·‘äº†
        ToolTip "ğŸ’¨"
    }
    SetTimer () => Tooltip(), -500
}

moveCursorLeftIfEndWithBracket(value) {

    if isEndWithSemicolonBracket(value) {
        Send "{Left 3}"
        return
    }

    if isEndWithCommaBracket(value) {
        Send "{Left 2}"
        return
    }

    if isEndWithCoupleBracket(value) {
        Send "{Left}"
        return
    }
    
    ; Java
    isEndWithSemicolonBracket(value) {
        HayStack := '");'
        needle := SubStr(value, -3, 3)
        return InStr(HayStack, needle, false, -1)

    }

    ; æ³¨è§£å­—ç¬¦ï¼šå¦‚ä»¥æ‹¬å·å’Œå­—ç¬¦ä¸²ç»“å°¾ï¼Œå…‰æ ‡å‘å·¦åç§»2ä½ï¼ˆSpringæ³¨è§£ï¼‰
    isEndWithCommaBracket(value) {
        HayStack := '")'
        needle := SubStr(value, -2, 2)
        return InStr(HayStack, needle, false, -1)
    }

    ; æˆå¯¹ç¬¦å·ï¼šå¦‚ä»¥æˆå¯¹ç¬¦å·(å³)ç»“å°¾ï¼Œå…‰æ ‡å‘å·¦åç§»1ä½
    isEndWithCoupleBracket(value) {
        HayStack := ")`]`}>ï¼‰ã€‰ã€‹ã€‘ï½â€™â€"
        needle := SubStr(value, -1, 1)
        return InStr(HayStack, needle, false, -1)
    }

}

execSemicolonAbbr(typo) {

    switch typo
    {
        case "no": Launcher.notepad()
        case "rex": Launcher.explorerReload()
        case "os": Run A_ScriptDir
        case "opc": Run "shell:my pictures"
        case "ow": Run "shell:Personal"
        case "or": Run "shell:RecycleBinFolder"
        case "ox": Run "shell:downloads"
        case "ob": Run "E:\backup"
        case "opr": Run "E:\projects"
        case "quit": SmartCloseWindow()
        case "ee": ToggleTopMost()
        case "oo": IME.toggle()	; å¦‚æœç¬¬ä¸€æ¬¡æ²¡åˆ‡æ¢æˆåŠŸï¼Œå°†å°±è¿æŒ‰åˆ‡æ¢
        case "tm": Run "taskmgr"
        case "sleep": DllCall("PowrProf\SetSuspendState", "Int", 0, "Int", 0, "Int", 0)
        case "reboot": slideToReboot()
        case "shutdown": slideToShutdown()
        case "spy": openWinSpy()
        case "dota": Dotfiles.apply()
        case "dotb": Dotfiles.backup()
        case "doto": Dotfiles.open()
        case "login": loginAdmin()
        case "dt": Send FormatTime(, "yyyy-MM-dd HH:mm:ss")
        case "hey": Sleep 500
        case "dh": Send "{Blind}^#{Left}"
        case "dl": Send "{Blind}^#{Right}"
        case "1": Launcher.dittoPaste(1)
        case "2": Launcher.dittoPaste(2)
        case "3": Launcher.dittoPaste(3)
        case "4": Launcher.dittoPaste(4)
        case "5": Launcher.dittoPaste(5)
        case "cc": Vim.EditJavaCommentTitle()
        case "il": Vim.inputChineseInDdoubleQuotes()	; å¼•å·ä¸­è¾“å…¥ä¸­æ–‡
        case "cil": Vim.changeCnCommentInDoubleQuotes()
        case "list": Vim.surroundWithList()
        case "tt": Vim.paste2LastLineUp()
        case "gg": Vim.paste2LastLineReplace()
        case "bb": Vim.paste2LastLineDown()
        case "vsa": Vim.swapArg()
        case "mm": Mouse.move()
        case "dbg": Debug.toggle()
        case "ln": IdeAction.markernext()
        case "lj": IdeAction.actionquickFix()
        case "lo": IdeAction.outlinefocus()
        case "lz": IdeAction.actiontoggleZenMode()
        case "lf": IdeAction.actionformatDocument()
        case "le": IdeAction.actionquickOpen()
        case "la": IdeAction.findInFiles()
        case "ls": IdeAction.viewexplorer()
        case "lgh": IdeAction.gitlensshowQuickFileHistory()
        case "lb": IdeAction.toggleBreakpoint()
        case "ldd": IdeAction.debugStart()
        case "lrr": IdeAction.rename()
        case "H": IdeAction.tabPreview()
        case "L": IdeAction.tabNext()
        case "docs": Website.docs()
        case "syl": Seeyon.login()
        case "syd": Seeyon.dev()
        case "sya": Seeyon.app()
        case "cxyl": Seeyon.chuXiongLogin()
        case "gen": Website.codeGen()
        case "json": Website.excel2json()
        case "gpt": Website.chatGpt()
        case "deveco": Startup.devEco()
        default:
            return false
    }
    return true
}

#Hotif EnableSemicolonMonkey
+A:: Send "{Blind}{text}â—" ; åˆ†ç‚¹è®ºè¿°çš„ç¬¦å·
*A::
{
    Send "{blind}*"
    return
}
*I::
{
    Send "{blind}:"
    return
}
*Space::
{
    send "{blind}{enter}"
    return
}
*,:: Send "{blind}{space}"	; å› ä¸ºç©ºæ ¼é”®ç»å¸¸è¯¯è§¦æ¢è¡Œ
+V:: Send "{blind}{text}ã€"	; ä¸­æ–‡é¡¿å·
*V:: Send "{blind}|"	; or
*Y:: Send "{blind}@"
*D:: Send "{blind}="	; é€»è¾‘åˆ¤æ–­
+S:: Send "{Blind}{text}â—‹" ; åˆ†ç‚¹è®ºè¿°äºŒçº§ç¬¦å·
*S:: Send "{blind}<"
*F:: Send "{blind}>"
*R:: Send "{blind}&"	; and
*G:: Send "{blind}{!}"	; not
*X:: Send "{blind}_"	; ä¸‹åˆ’çº¿
*H:: Send "{blind}`%"	; åŒ¹é…æˆå¯¹çš„æ‹¬å·
+Q:: Send "{blind})"	; å¼ºè¿«ç—‡è¡¨ç¤ºè¦æ”¾ä¸€èµ·
*Q:: Send "{blind}("
*U:: Send "{blind}$"	; å¥å­å
*E:: Send "{blind}{^}"	; å¥å­å‰ï¼Œä¸Šä¸­å·¦å³
+J:: Send "{blind}{text}ï¼›"	; ä¸­æ–‡åˆ†å·ï¼Œå¤§äººä»€ä¹ˆéƒ½è¦
*J:: Send "{blind}{text};"	; è‹±æ–‡åˆ†å·ï¼Œå¸¸ç”¨
*C:: Send "{blind}."	; å­˜ç–‘ï¼Œä¸€ä¸ªèº«ä½å¯ä»¥åˆ°è¾¾ï¼Œè®¾ç½®2ä¸ªé”®ï¼Œè¿‡åº¦è®¾è®¡ï¼Ÿ.=, copyå¤ç”¨ä¸Šæ¬¡æ“ä½œ
+B:: Send "{blind}{}}"	; æˆå¯¹åœ¨ä¸€èµ·
*B:: Send "{blind}{{}"	; viB,viqé…åˆå¾ˆå¥½
*K:: Send "{blind}``"	; ç²¾å‡†å®šä½æ—¶
*L:: Send "{blind}`""	; å­—ç¬¦ä¸², å¯„å­˜å™¨
*W:: Send "{blind}{#}"
*N:: Send "{blind}-"	; new/æ–°å¢/å¢åŠ 
*M:: Send "{blind}{+}"	; minus/å‡å°‘
+T:: Send "{blind!+}{Space 4}"	; ç±»ä¼¼ tab
*T:: Send "{blind}~"	; ç»ˆç«¯ç”¨æˆ·æ ¹ç›®å½•
*Z:: Send "{blind}^z"	; æ’¤å›ï¼Œç›¸å½“äºåˆ å­—
*O:: IME.toggle()	; ç»ˆç«¯å¯ç”¨ Esc åˆ‡æ¢å›è‹±æ–‡ï¼Œä»»æ„åœºåˆç”¨ Rshift åˆ‡æ¢ï¼Œæˆ–å†æŒ‰ä¸€é
*1:: chineseComment("//")
*2:: chineseComment(";")
*3:: chineseComment("###")
#Hotif

;=====================================================================o
;                     Hook Funtions

SendAndLeft(text, offset := 1)
{
    Send "{text}" text
    Send "{Left " offset "}"
}

chineseComment(startStr := "") {
    Send "{text}" startStr " "
    IME.set("ä¸­æ–‡")
}

onTypoChar(ih, char) {
    ; åˆ é™¤ç¼“å­˜æ‹¼å†™æ—¶ï¼Œä¹Ÿä¸ä¼šæ›´æ–°
    Tooltip ih.Input
}

onTypoEnd(ih) {
    ; æ¸…ç©ºç¼“å­˜ï¼Œå¯èƒ½é®è”½è¡¨ç°ï¼šæœ‰æ—¶æ–°å‘½ä»¤é—ç•™é”™è¯¯ä¸Šæ¬¡é”™è¯¯ä¿¡æ¯
    Tooltip
    ; typoTip.show(ih.Input)
}

slideToShutdown()
{
    run "SlideToShutDown"
    sleep 1300
    MouseClick "Left", 100, 100
}

slideToReboot()
{
    ; run, SlideToShutDown
    ; sleep, 1300
    ; MouseClick, Left, 100, 100
    ; sleep, 250
    shutdown 2
}

ToggleTopMost()
{
    exStyle := WinGetExStyle("A")
    if (exStyle & 0x8)	; 0x8 ä¸º WS_EX_TOPMOST
    {
        exStyle := "  å–æ¶ˆç½®é¡¶  "
        WinSetAlwaysOnTop False, "A"
    } else {
        exStyle := "  çª—å£ç½®é¡¶  "
        WinSetAlwaysOnTop True, "A"
    }
    tip(exStyle, -500)
}

getKeyStr(dict) {
    string := ""
    for key, value in dict
    {
        string .= key ","
    }
    return RTrim(string, ",")

}