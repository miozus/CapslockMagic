SendMode "Input"
SetWorkingDir A_ScriptDir


;=====================================================================o
;                      Punctuation Hook Registry

global PunctuationMode := false
; all dict comes from database
semicolonHookStr := getKeyStr(secretDictionary) "," getKeyStr(userDictionary) 
semicolonHook := InputHook("C", "{Space}{Esc}", semicolonHookStr)
semicolonHook.OnChar := onTypoChar
semicolonHook.OnEnd := onTypoEnd

+`;:: Send "{blind}:"
*`;::
{
    global semicolonHook, PunctuationMode
    thisHotkey := A_ThisHotkey
    disableOtherHotkey(thisHotkey)
    PunctuationMode := true
    KeyWait ";"
    PunctuationMode := false
    if (A_PriorKey == ";" && A_TimeSinceThisHotkey < 350)
    {
        enterSemicolonAbbr(semicolonHook)
    } 
    enableOtherHotkey(thisHotkey)
}

; è‡ªå®šä¹‰è¾“å…¥æ³•
; ä¼˜å…ˆï¼šæ— å‘½ä»¤è¯å…¸ï¼ˆçº¯æ–‡å­—ï¼‰ï¼›ä¼˜å…ˆæ‰“å°è‡ªå®šä¹‰çŸ­è¯­ï¼Œæ”¯æŒè·¨å¹³å°å¯¼å‡ºï¼Œè¾“å…¥æ³•é€šç”¨
; å…¶æ¬¡: è‡ªå®šä¹‰è„šæœ¬åŠŸèƒ½
;
; ä¸€ç§è§‚ç‚¹è®¤ä¸ºï¼Œå®˜æ–¹æ–‡æ¡£è¯´ï¼Œswitch å‘½ä¸­å°±è¿”å›ï¼Œæ²¡æœ‰è½ä¸‹ç›´é€šï¼Œæ‰€ä»¥ä¸ç”¨ return
; ä¸€ç§è§‚ç‚¹è®¤ä¸ºï¼Œå‘½ä¸­åè¿”å›ï¼Œå¯ä»¥æå‰ç»“æŸ hook ç›‘å¬è¾“å…¥ï¼Œé˜²æ­¢å‚ç›´è½ä¸‹ã€‚
enterSemicolonAbbr(ih) {
    ToolTip "ğŸ™ˆ"   
    ih.start()
    ih.wait()
    ih.stop()
    if (ih.Match)
    {
        try {
            value := "{text}" userDictionary[ih.Match]
            Send value
            if isEndWithCommaBracket(value) {
                Send "{Left 2}"
                return
            }
            if isEndWithCoupleBracket(value) {
                Send "{Left}"
                return
            }
            return
        }
        ; å¸¦å‘½ä»¤è¯å…¸: åŠŸèƒ½æ¯”è¾ƒé‡
        ToolTip "ğŸ™‰ " secretDictionary[ih.Match] 
        execSemicolonAbbr(ih.Match)
    } else {
        ; æœªæ”¶å½•è¯å…¸ï¼ŒçŒ´å­è·‘äº†
        ToolTip "ğŸ’¨"
    }
    SetTimer () => Tooltip(), -500
}


; æ³¨è§£å­—ç¬¦ï¼šå¦‚ä»¥æ‹¬å·å’Œå­—ç¬¦ä¸²ç»“å°¾ï¼Œå…‰æ ‡å‘å·¦åç§»2ä½ï¼ˆSpringæ³¨è§£ï¼‰
isEndWithCommaBracket(value) {
    HayStack := '")'
    needle := SubStr(value, -2, 2)
    return InStr(HayStack, needle, false, -1)
}

; æˆå¯¹ç¬¦å·ï¼šå¦‚ä»¥æˆå¯¹ç¬¦å·(å³)ç»“å°¾ï¼Œå…‰æ ‡å‘å·¦åç§»1ä½
isEndWithCoupleBracket(value) {
    HayStack := ")`]`}>ï¼‰ã€‰ã€‹ã€‘ï½â€™â€"
    needle := SubStr(value, -1, 1) 
    return InStr(HayStack, needle, false, -1)
}

execSemicolonAbbr(typo) {

    switch typo 
    {
        case "no":
            path := "notepad"
            pasteInNotepad()
            ActivateOrRun("è®°äº‹æœ¬", path, "", "")
            return
        case "rex":
            path := "tools\é‡å¯èµ„æºç®¡ç†å™¨.exe"
            ActivateOrRun("", path, "", "")
            return
        case "dm": Run A_ScriptDir
        case "dpc": Run "shell:my pictures"
        case "dw": Run "shell:Personal"
        case "dr": Run "shell:RecycleBinFolder"
        case "dx": Run "shell:downloads"
        case "db": Run "E:\backup"
        case "dpr": Run "E:\projects"
        case "qq": SmartCloseWindow()
        case "ee": ToggleTopMost()
        case "oo": switchIME() ; å¦‚æœç¬¬ä¸€æ¬¡æ²¡åˆ‡æ¢æˆåŠŸï¼Œå°†å°±è¿æŒ‰åˆ‡æ¢
        case "om": inputChineseInDdoubleQuotes() ; å¼•å·ä¸­è¾“å…¥ä¸­æ–‡
        case "gcam":
            gitStr := "git add -A`; git commit -a -m ''`; git push origin (git branch --show-current)`;"
            SendAndLeft(gitStr, 47)
        case "tm": Run "taskmgr"
        case "sleep": DllCall("PowrProf\SetSuspendState", "Int", 0, "Int", 0, "Int", 0)
        case "reboot": slideToReboot()
        case "shutdown": slideToShutdown()
        case "spy": openWinSpy()
        case "dotb": backupDotfiles()
        case "doto": 
            path := A_Programs "\Visual Studio Code\Visual Studio Code.lnk"
            activateOrRun("dotfiles ahk_exe Code.exe", path,  "E:\dotfiles")
        case "login": loginAdmin()
        case "dt": Send FormatTime(,"yyyy-MM-dd HH:mm:ss")
        case "hey": Sleep 500
        case "dh": Send "{Blind}^#{Left}"
        case "dl": Send "{Blind}^#{Right}"
        case "1": Send "{Blind}^+!1"
        case "2": Send "{Blind}^+!2"
        case "3": Send "{Blind}^+!3"
        case "4": Send "{Blind}^+!4"
        case "5": Send "{Blind}^+!5"
        case "cc": EditJavaCommentTitle()
        case "cil": changeCnCommentInDoubleQuotes()
        case "list": surroundWithList() 
        case "tt": paste2LastLineUp()
        case "gg": paste2LastLineReplace()
        case "bb": paste2LastLineDown()
        default: 
            return false
    }
    return true
}

#Hotif PunctuationMode
*A::
{
Send "{blind}*"
return
}
*I::
{
Send "{blind}:"
return
}
*Space::
{
send "{blind}{enter}"
return
}
+V::Send "{blind}{text}ã€"    ; ä¸­æ–‡é¡¿å·
*V::Send "{blind}|"    ; or
*Y::Send "{blind}@"
*D::Send "{blind}="    ; é€»è¾‘åˆ¤æ–­
*S::Send "{blind}<"
*F::Send "{blind}>"
*R::Send "{blind}&"    ; and
*G::Send "{blind}{!}"  ; not
*X::Send "{blind}_"    ; ä¸‹åˆ’çº¿
*H::Send "{blind}`%"   ; åŒ¹é…æˆå¯¹çš„æ‹¬å·
*Q::Send "{blind}("
*U::Send "{blind}$"    ; å¥å­å
*E::Send "{blind}{^}"  ; å¥å­å‰ï¼Œä¸Šä¸­å·¦å³
+J::Send "{blind}{text}ï¼›" ; ä¸­æ–‡åˆ†å·ï¼Œå¤§äººä»€ä¹ˆéƒ½è¦
*J::Send "{blind}{text};" ; è‹±æ–‡åˆ†å·ï¼Œå¸¸ç”¨
*C::Send "{blind}."    ; å­˜ç–‘ï¼Œä¸€ä¸ªèº«ä½å¯ä»¥åˆ°è¾¾ï¼Œè®¾ç½®2ä¸ªé”®ï¼Œè¿‡åº¦è®¾è®¡ï¼Ÿ.=, copyå¤ç”¨ä¸Šæ¬¡æ“ä½œ
*B::Send "{blind}{{}"  ; viB,viqé…åˆå¾ˆå¥½
*K::Send "{blind}``"   ; ç²¾å‡†å®šä½æ—¶
*L::Send "{blind}`""   ; å­—ç¬¦ä¸², å¯„å­˜å™¨
*W::Send "{blind}{#}"
*N::Send "{blind}-"  ; new/æ–°å¢/å¢åŠ 
*M::Send "{blind}{+}"    ; minus/å‡å°‘
+T::Send "{blind!+}{Space 4}" ; ç±»ä¼¼ tab 
*T::Send "{blind}~"   ; ç»ˆç«¯ç”¨æˆ·æ ¹ç›®å½•
*Z::Send "{blind}^z"         ; æ’¤å›ï¼Œç›¸å½“äºåˆ å­—
*O::switchIME()  ; ç»ˆç«¯å¯ç”¨ Esc åˆ‡æ¢å›è‹±æ–‡ï¼Œä»»æ„åœºåˆç”¨ Rshift åˆ‡æ¢ï¼Œæˆ–å†æŒ‰ä¸€é
*1::chineseComment("//")
*2::chineseComment(";")
*3::chineseComment("###")
#Hotif 

;=====================================================================o
;                     Hook Funtions

SendAndLeft(text, offset:=1)
{
    Send "{text}" text
    Send "{Left " offset "}"
}

chineseComment(startStr:="") {
    Send "{text}" startStr " "
    setIME("ä¸­æ–‡")
}

onTypoChar(ih, char) {
    ; åˆ é™¤ç¼“å­˜æ‹¼å†™æ—¶ï¼Œä¹Ÿä¸ä¼šæ›´æ–°
    Tooltip ih.Input
}

onTypoEnd(ih) {
    ; æ¸…ç©ºç¼“å­˜ï¼Œå¯èƒ½é®è”½è¡¨ç°ï¼šæœ‰æ—¶æ–°å‘½ä»¤é—ç•™é”™è¯¯ä¸Šæ¬¡é”™è¯¯ä¿¡æ¯
    Tooltip 
    ; typoTip.show(ih.Input)
}

slideToShutdown()
{
    run "SlideToShutDown"
    sleep 1300
    MouseClick "Left", 100, 100
}

slideToReboot()
{
    ; run, SlideToShutDown
    ; sleep, 1300
    ; MouseClick, Left, 100, 100
    ; sleep, 250
    shutdown 2
}


ToggleTopMost()
{
    exStyle := WinGetExStyle("A")
    if (exStyle & 0x8)  ; 0x8 ä¸º WS_EX_TOPMOST
    {
        exStyle := "  å–æ¶ˆç½®é¡¶  "
        WinSetAlwaysOnTop False, "A"
    }
    else {
        exStyle := "  çª—å£ç½®é¡¶  "
        WinSetAlwaysOnTop True, "A"
    }
    tip(exStyle, -500)
}

getKeyStr(dict) {
    string := ""
    for key, value in dict
    {
        string .= key ","
    }
    return RTrim(string, ",")
}
inputChineseInDdoubleQuotes()  {
    Send "{Text}`"`""
    Send "{Left}"
    setIME("ä¸­æ–‡")
}

changeCnCommentInDoubleQuotes(){
    Send '{Blind}ci"'
    setIME("ä¸­æ–‡")
}

; vim micro
EditJavaCommentTitle() {
    Send "{Text}{2j2wC"
    Send "{Bind}^{Space}"
}

surroundWithList() {
    Send "{Bind}{Esc}ciw"
    Send "{Text}List<"
    Send '{Bind}^r">'
}

paste2LastLineUp() {
    Send "{Bind}^o"
    Sleep 100
    Send "{Text}P"
}

paste2LastLineDown() {
    Send "{Bind}^o"
    Sleep 100
    Send "{Text}p"
}

paste2LastLineReplace() {
    Send "{Bind}^o"
    Sleep 100
    Send "{Text}Vp"
}