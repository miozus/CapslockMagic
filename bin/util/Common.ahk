SendMode "Input"	; Recommended for new scripts due to its superior speed and reliability.
SetWorkingDir A_ScriptDir	; Ensures a consistent starting directory.

;=====================================================================o
;                    Common Utilities
;---------------------------------------------------------------------o

;=====================================================================o
;                    Activate Or Run
;---------------------------------------------------------------------o
; ÁºìÂ≠ò
global last_winFilter := ""
global shell := ""
;---------------------------------------------------------------------o
ActivateOrRun(to_activate := "", target := "", args := "", workingdir := "", RunAsAdmin := false)
{
    to_activate := Trim(to_activate)
    ; WinShow, %to_activate%
    if (to_activate && winexist(to_activate)) {
        MyGroupActivate(to_activate)
    } else if (target != "")
    {
        ;showtip("not exist, try to start !")
        if (RunAsAdmin)
        {
            if (substr(target, 1, 1) == "\")
                target := substr(target, 2, strlen(target) - 1)
            Run target args, WorkingDir
        } else
        {
            oldTarget := target
            target := WhereIs(target)
            if (target)
            {
                if (SubStr(target, -3) != ".lnk") {
                    ShellRun(target, args, workingdir)
                } else {
                    ; Ê£ÄÊü• lnk ÊòØÂê¶ÊçüÂùè
                    FileGetShortcut target, &OutTarget
                    ; if FileExist(OutTarget)
                    ShellRun(target, args, workingdir)
                }
            } else {
                try
                {
                    if (workingdir && args) {
                        Run oldTarget args, workingdir
                    } else if (workingdir) {
                        Run oldTarget, workingdir
                    } else if (args) {
                        Run oldTarget args
                    } else {
                        Run oldTarget
                    }
                } catch Error
                {
                    tip(Error.Message)
                }
            }
            ; ÊúâÁöÑÂ∫îÁî®ÂêØÂä®ÔºåÁ´üÁÑ∂‰∏ç‰ºöÁΩÆÈ°∂ÔºåÊâÄ‰ª•ÈúÄË¶ÅÊøÄÊ¥ª‰∏Ä‰∏ã
            ; if (to_activate) {
                ; Sleep 1000
                ; WinActivate to_activate
            ; }
        }
    }
}
;---------------------------------------------------------------------o
/*
    ÈÄöËøá shell ÈôçÊùÉÔºå‰ª•ÊôÆÈÄöÁî®Êà∑ÊùÉÈôêÂêØÂä®Â∫îÁî®
    ShellRun by swagfag 2021/04/24
    Requires AutoHotkey v2.0-a136-feda41f4
    https://www.autohotkey.com/boards/viewtopic.php?t=78190
 */
ShellRunApp(prms*)
{
    shellWindows := ComObject("Shell.Application").Windows
    desktop := shellWindows.FindWindowSW(0, 0, 8, 0, 1)	; SWC_DESKTOP, SWFO_NEEDDISPATCH

    ; Retrieve top-level browser object.
    if tlb := ComObjQuery(desktop,
        "{4C96BE40-915C-11CF-99D3-00AA004AE837}",	; SID_STopLevelBrowser
        "{000214E2-0000-0000-C000-000000000046}")	; IID_IShellBrowser
    {
        ; IShellBrowser.QueryActiveShellView -> IShellView
        if ComCall(15, tlb, "ptr*", &psv := 0) = 0
        {
            ; Define IID_IDispatch.
            IID_IDispatch := Buffer(16)
            NumPut("int64", 0x20400, "int64", 0x46000000000000C0, IID_IDispatch)

            ; IShellView.GetItemObject -> IDispatch (object which implements IShellFolderViewDual)
            ComCall(15, psv, "uint", 0, "ptr", IID_IDispatch, "ptr*", &pdisp := 0)

            ; Get Shell object.
            shell := ComObjFromPtr(pdisp).Application

            ; IShellDispatch2.ShellExecute
            shell.ShellExecute(prms*)

            ObjRelease(psv)
        }
    }
}

;---------------------------------------------------------------------o
; üì¶ ÁºìÂ≠òÊú∫Âà∂
ShellRun(prms*)
{
    global shell
    try {
        try {
            if (shell) {
                shell.ShellExecute(prms*)
                return
            }
        } catch {
            tip("refresh shell cache")
        }
        ShellRunApp(prms*)
    } catch Error as e {
        MsgBox e.Message
        tip("shell failed")
    }
}
;---------------------------------------------------------------------o
GetProcessName(id := "") {
    if (id == "")
        id := "A"
    else
        id := "ahk_id " . id

    name := WinGetProcessName(id)
    if (name == "ApplicationFrameHost.exe") {
        ;ControlGet hwnd, Hwnd,, Windows.UI.Core.CoreWindow, %id%
        hwnd := ControlGetHwnd("Windows.UI.Core.CoreWindow1", id)
        if hwnd {
            name := WinGetProcessName("ahk_id " hwnd)
        }
    }
    return name
}

HasVal(haystack, needle)
{
    if !(IsObject(haystack)) || (haystack.Length = 0)
        return 0
    for index, value in haystack
        if (value = needle)
            return index
    return 0
}

WinVisible(id)
{
    ;WingetPos x, y, width, height, ahk_id %id%
    title := WinGetTitle("ahk_id " id)
    ;WinGet, state, MinMax, ahk_id %id%
    ;tooltip %x% %y% %width% %height%

    ;sizeTooSmall := width < 300 && height < 300 && state != -1 ; -1 is minimized
    empty := !Trim(title)
    ;if (!sizeTooSmall && !empty)
    ;    tooltip %x% %y% %width% %height% "%title%"

    return empty ? 0 : 1
    ;return  sizeTooSmall || empty  ? 0 : 1
}

GetVisibleWindows(winFilter)
{
    ids := []

    id := WinGetList(winFilter, , "Program Manager")
    for this_id in id{
        if (WinVisible(this_id))
        {
            ids.push(this_id)
        }
    }
    if (ids.length == 0)
    {
        pos := Instr(winFilter, "ahk_exe") - StrLen(winFilter) + StrLen("ahk_exe")
        pname := Trim(Substr(winFilter, pos))
        id := WinGetList("ahk_class ApplicationFrameWindow")
        for this_id in id {
            get_name := GetProcessName(this_id)
            if (get_name == pname) {
                ids.push(this_id)
            }
        }
    }
    return ids
}

WhereIs(FileName)
{
    ; Ê†°È™åË∑ØÂæÑÁ∫øÁ¥¢ÔºåÊ∑±ÂÖ•Á≥ªÁªüÂ§ö‰∏™ÁõÆÂΩïÔºåÁõ¥Âà∞ÊâæÂà∞ÂèØÊâßË°åÁöÑÊñá‰ª∂ÂêçÁß∞
    ; https://autohotkey.com/board/topic/20807-fileexist-in-path-environment/

    ; Working Folder
    PathName := A_WorkingDir "\"
    if (FileExist(PathName FileName)) {
        Return PathName FileName
    }

    ; absolute path
    if (FileExist(FileName)) {
        Return FileName
    }

    ; Parsing DOS Path variable
    DosPath := EnvGet("Path")
    Loop Parse DosPath, "`;"
    {
        if (A_LoopField == "") {
            Continue
        }
        if (FileExist(A_LoopField "\" FileName)) {
            Return A_LoopField "\" FileName
        }
    }

    ; Looking up Registry
    PathName := RegRead("HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\App Paths\", FileName)
    if (FileExist(PathName)) {
        Return PathName
    }

}

GroupAddId(&GroupName, p1 := "", p2 := "", p3 := "", p4 := "", p5 := "")
{
    static g := 1
    If (GroupName == "") {
        GroupName := "AutoName" g++
    }
    GroupAdd GroupName, p1, p2 p3 p4 p5
}

MyGroupActivate(winFilter)
{

    winFilter := Trim(winFilter)
    if (!winactive(winFilter))
    {
        activateFirstVisible(winFilter)
        return
    }

    ; group ÊòØÁ™óÂè£ÁªÑÂØπË±°, Ëøô‰∏™ÂØπË±°Êó†Ê≥ïËé∑ÂèñÂÜÖÈÉ®Áä∂ÊÄÅ, ÊâÄ‰ª•Áî® win_group_array_form Êù•ÂÇ®Â≠ò‰ªñÁöÑÁä∂ÊÄÅ
    global win_group
    global win_group_array_form
    global last_winFilter

    ; Âà§Êñ≠ÊòØÂê¶ËøõÂÖ•‰∫ÜÊñ∞ÁöÑÁ™óÂè£ÁªÑ
    ; ÁºìÂ≠òÊú∫Âà∂ÔºåÂàùÂßãÂÄº‰∏∫Á©∫
    if (winFilter != last_winFilter)
    {
        last_winFilter := winFilter
        win_group_array_form := []
        win_group := ""	; Âª∫Á´ãÊñ∞ÁöÑÂàÜÁªÑ
    }

    ; ÂØπÊØî‰∏ä‰∏ÄÊ¨°ÁöÑÁä∂ÊÄÅ, Ëé∑ÂèñÊñ∞ÁöÑÁ™óÂè£, ÁÑ∂ÂêéÊääÊñ∞Á™óÂè£Ê∑ªÂä†Âà∞ win_group_array_form Áä∂ÊÄÅÂíå win_group
    curr_group := GetVisibleWindows(winFilter)
    for val in curr_group
    {
        ; val := curr_group[A_Index]
        if (!HasVal(win_group_array_form, val))
        {
            win_group_array_form.push(val)
            GroupAddId(&win_group, "ahk_id " val)
        }
    }

    GroupActivate win_group, "R"
}

SwitchWindows()
{
    winClass := WinGetClass("A")
    if (winClass == "ApplicationFrameWindow") {
        title := WinGetTitle("A")
        to_check := title " ahk_class ApplicationFrameWindow"
    } else
        to_check := "ahk_exe " GetProcessName()

    MyGroupActivate(to_check)
    return
}

IsBrowser(pname)
{
    Haystack := "chrome.exe,MicrosoftEdge.exe,firefox.exe,360se.exe,opera.exe,iexplore.exe,qqbrowser.exe,sogouexplorer.exe"
    return InStr(Haystack, pname, false)
}

SmartCloseWindow()
{
    if (winactive("ahk_class WorkerW ahk_exe explorer.exe"))
        return

    thisClass := WinGetclass("A")
    name := GetProcessName()
    if IsBrowser(name)
        Send "^w"
    else if WinActive("- Microsoft Visual Studio ahk_exe devenv.exe")
        Send "^{f4}"
    else
    {
        if (thisClass == "ApplicationFrameWindow" || name == "explorer.exe")
            Send "!{f4}"
        else
        ; WM_SYSCOMMAND
            PostMessage 0x112, 0xF060, , , "A"
    }
}

activateFirstVisible(windowSelector)
{
    id := firstVisibleWindow(windowSelector)
    ; WinGet, State, MinMax, ahk_id %id%
    ; if (State = -1)
    ;     WinRestore, ahk_id %id%
    WinActivate "ahk_id " id
}

firstVisibleWindow(windowSelector)
{
    winList := WinGetList(windowSelector)
    for this_id in winList
    {
        ; item := winList A_Index
        title := WinGetTitle(this_id)
        if (Trim(title) != "") {
            return this_id
        }
    }
}

disableOtherHotkey(thisHotkey)
{
    global allHotkeys, chinesePunctuationHotkey := false
    for index, value in allHotkeys
    {
        if (value != thisHotkey) {
            hotkey value, "off"
        }
    }
}

enableOtherHotkey(thisHotkey)
{
    global allHotkeys, chinesePunctuationHotkey := true
    for index, value in allHotkeys
    {
        if (value != thisHotkey) {
            hotkey value, "on"
        }
    }
}

;=====================================================================o
;                    Utils Macro
;---------------------------------------------------------------------o
; ÊøÄÊ¥ªÊàñËøêË°å, ÈªòËÆ§ÂàùÂßãËã±ÊñáËæìÂÖ•Ê≥ïÔºàÁúÅÁï•‰∏çÂ°´ÂèÇÊï∞ÔºõÂ¶ÇÈúÄ‰∏≠ÊñáÔºåÂÜçÂä†ÈÄóÂè∑Âíå‚Äú‰∏≠Êñá‚ÄùÔºåË¶ÅÂåÖÂê´ÂèåÂºïÂè∑Ôºâ
; @Deprecated
activateOrRunOld(title, exe_path, language := "EN") {
    if WinExist(title) {
        WinActivate
    } else {
        Run exe_path
    }
    setIME(language)	; ÈªòËÆ§‰ª•Â∞èÂÜôÂºÄÂßã; posh Â§±ÂéªÊéßÂà∂ÊùÉÔºåË¶Å‰∏ã‰∏§‰∏ãÊâçËÉΩ‰øùÊåÅËã±Êñá
}
;---------------------------------------------------------------------o
openWinSpy() {
    ; ÁâàÊú¨‰∏çÂÖºÂÆπÁöÑÁóõ
    activateOrRunOld("ahk_exe WindowSpy.exe", "D:\TOOLS\AutoHotKey\AutoHotKey_L-V1\AutoHotkeyU64.exe D:\TOOLS\AutoHotKey\AutoHotKey_L-V1\WindowSpy.ahk")
}

; ÂçïÂáªÈº†Ê†áÂè≥ÈîÆÔºå‰ª•Á¨¨ N ‰∏™ÊñπÂºèÊâìÂºÄËØ•Êñá‰ª∂
openByAppKey(sort := 0) {
    tip(sort--, -1000)
    Send "{AppsKey}"
    Send "{down " sort "}"
    Send "{Enter}"
}
;---------------------------------------------------------------------o
; ÁÆÄÂçïÁöÑÂêéÂè∞ÁôªÈôÜÊìç‰ΩúÔºåËæìÂÖ•È™åËØÅÁ†ÅÊó∂ÔºåËæìÂÖ•Ê≥ïËÆæ‰∏∫Ëã±Êñá
loginAdmin() {
    Send "{text}admin"
    Send "{Tab}"
    Send "{text}admin"
    Send "{Tab}"
    setIME("EN")
}
;---------------------------------------------------------------------o
; Â§çÂà∂ÊñáÂ≠óÁî®ËÆ∞‰∫ãÊú¨Êü•Áúã
pasteInNotepad()
{
    Send "^c"
    if WinExist("ahk_class Notepad")
    {
        WinActivate
    } else
    {
        Run "Notepad"
        WinWait "ahk_class Notepad"
        WinActivate
    }
    Send "^a^v"
}
;---------------------------------------------------------------------o
copySelectWord() {
    sendEvent "^{Left}^+{Right}"
    Send "^c"
}
;---------------------------------------------------------------------o
getSelectString() {
    Send "^c"
    return A_Clipboard
}

;=====================================================================o
;                     Tooltip Packing
;---------------------------------------------------------------------o
; ÊÇ¨ÊµÆÊèêÁ§∫Áä∂ÊÄÅ‰ø°ÊÅØÁöÑÂ∞ÅË£Ö
tip(message, duration := -1000)
{
    ToolTip message
    SetTimer closeToolTip, duration
}

closeToolTip()
{
    ToolTip
}

debugModifierKey() {
    ToolTip (
        "> LAlt" GetKeyState("LAlt")
        " LWin" GetKeyState("LWin")
        " LCtrl" GetKeyState("LCtrl")
    )
    SetTimer () => ToolTip(), -1000
}
;---------------------------------------------------------------------o


; üß™ ÂºÄÂèëÂ∑•ÂÖ∑
; ---
; ËæÖÂä©ÂÜô‰ª£Á†ÅÁöÑÈõÜÂêàÔºöÂèñËâ≤ÔºåÂèñÂùêÊ†áÔºåÂ∫èÂàóÂåñ‰∫åÁª¥Êï∞ÁªÑÔºàÂÇ®Â≠òÊï∞ÊçÆÂ∫ìÔºâ
class DevUtils {

   static serilize(nestedObj) {
        inner := "", outer := ""
        for k1, nested in nestedObj.OwnProps() {
            for k2, v2 in nested.OwnProps() {
                inner .= k2 ':"' v2 '", '
            }
            outer .= k1 ': {' Rtrim(inner, ", ") '},`n'
            inner := ""
        }
        outer := "{`n" Rtrim(outer, ",`n") "`n}"
        A_Clipboard := outer
        msgbox outer, "Â∑≤Êã∑Ë¥ù"
        return outer
    }

    static getPixelSearchCode(d := 5) {
        MouseGetPos & x, &y
        msg := x - d ", " y - d ", " x + d ", " y + d ", " PixelGetColor(x, y)
        A_clipboard := "PixelSearch(&FoundX, &FoundY, " msg ", 3)"
        Tooltip msg " Â∑≤Êã∑Ë¥ù"
        SetTimer () => ToolTip(), -2000
    }

    static getMousePosCode() {
        MouseGetPos & x, &y
        msg := x " " y
        A_clipboard := 'Click "' msg '"'
        Tooltip msg " Â∑≤Êã∑Ë¥ù"
        SetTimer () => ToolTip(), -2000
    }
    
}

backupDotfiles() {
    ;  ahk Êú¨ËÑöÊú¨Áõ¥Êé•Âú® dot È°πÁõÆ‰∏≠(Âø´Êç∑ÊñπÂºè)‰ΩøÁî®ÂíåÁª¥Êä§Ôºå‰∏çÂÜçÂè¶Â§ñÂ§á‰ªΩÊú¨Âú∞ÂíåËΩ¨Áßª‰∫Ü
    ; C:\Users\miozus\AppData\Roaming\JetBrains\IntelliJIdea2021.3\options\postfixTemplates.xml
    ; C:\Users\miozus\AppData\Roaming\JetBrains\IntelliJIdea2021.3\jba_config\templates\Java.xml
    A_HOME := Rtrim(A_AppData, "AppData\Roaming")
    dotfiles := "E:\dotfiles"

    nvim := A_HOME "\AppData\Local\nvim\init.vim"
    FileCopy nvim, dotfiles "\.config\nvim\init.vim", true

    ideaFolder := A_AppData "\JetBrains\IntelliJIdea2022.1"
    ; DirCopy ideaFolder "\jba_config\win.keymaps", dotfiles "\apps\Idea\keymaps", true
    ; FileCopy ideaFolder "\options\postfixTemplates.xml", dotfiles "\apps\Idea\snippets", true

    FileCopy A_AppData "\Code\User\sync", dotfiles "\apps\Code", true 
    ; codeFolders := ["sync\keybindings\preview", "snippets"]
    ; for folder in codeFolders
    ; {
        ; folder := "\Code\User\" folder
        ; DirCopy A_AppData folder, dotfiles "\apps" folder, true
    ; }

    configFiles := [".ideavimrc", ".ipython\profile_default\ipython_config.py"]
    ; configFiles := [".ideavimrc", ".vimrc", ".tmux.conf", ".ipython\profile_default\ipython_config.py"]
    for file in configFiles
    {
        ; Â¶ÇÊûúÂÖ∂‰∏≠‰∏Ä‰∏™Êñá‰ª∂Â§π/Êñá‰ª∂Ôºå‰∏çÂ≠òÂú®Ôºå‰ºöÊä•Èîô
        FileCopy A_HOME "\" file, dotfiles "\" file, true
    }
    pshrc := A_MyDocuments "\WindowsPowerShell\Microsoft.PowerShell_profile.ps1"
    FileCopy pshrc, dotfiles "\Windows\Microsoft.PowerShell_profile.ps1", true

    MsgBox "Êú¨Êú∫ÈÖçÁΩÆÂ∑≤Â§á‰ªΩ"
}
;---------------------------------------------------------------------o

; Êñá‰ª∂ÁÆ°ÁêÜÂ∑•ÂÖ∑ÔºöËØªÂÜôÔºåÊñá‰ª∂ÂêçÊ∑ªÂä†Êó•Êúü
class FileUtils {


    static outputAs(textStr, fileName)
{
        if (FileName == "" || textStr == "") {
            msgbox "‰º†ËæìÊï∞ÊçÆ/Êñá‰ª∂ÂêçÁß∞‰∏∫Á©∫ÔºåÊó†Ê≥ïÂØºÂá∫"
            return
        }
        try {
            DirCreate ".\dist"
            FileName := ".\dist\" this.fileNameAddToday(fileName)
    }
        catch Error as err {
            ToolTip err.Message
            return
}
    ; FileName := FileSelect("S16",, "Create a new file:")
    ; connect other script
        absoluteDir := A_WorkingDir LTrim(FileName, ".") 
        A_Clipboard := absoluteDir

    try
    ; Ë¶ÅÊâìÂºÄÊñá‰ª∂ÁöÑË∑ØÂæÑ, Â¶ÇÊûúÊú™ÊåáÂÆöÁªùÂØπË∑ØÂæÑÂàôÂÅáÂÆöÂú® A_WorkingDir ‰∏≠.
        FileObj := FileOpen(FileName, "w")
    catch Error
    {
            MsgBox "Êó†Ê≥ïÊâìÂºÄÊñá‰ª∂ " FileName 
                . "`n`n" Type(err) ": " err.Message
                . "`n`n ËØ∑Ê£ÄÊü•Êñá‰ª∂Ë∑ØÂæÑÊàñÂêçÁß∞ÊòØÂê¶Ê≠£Á°Æ" 
        return
    }
        try {
    ; ÈÄöËøáËøôÁßçÊñπÂºèÂÜôÂÖ•ÂÜÖÂÆπÂà∞Êñá‰ª∂Êó∂, Ë¶Å‰ΩøÁî® `r`n ËÄå‰∏çÊòØ `n Êù•ÂºÄÂßãÊñ∞Ë°å.
    FileObj.Write(textStr)
    FileObj.Close()
        msg := 
        (
            '1.ÂΩìÂâçÁ≥ªÁªüÁºñÁ†Å‰∏∫ ' FileObj.Encoding 
            '`n`nÊ≥®ÊÑèÔºöÂè™Êúâ UTF-8 ÊîØÊåÅ‰øùÁïô emojiÔºà‰øÆÊîπÊñπÊ≥ïÔºöÁ≥ªÁªüËÆæÁΩÆ > ÊêúÁ¥¢ÁÆ°ÁêÜËØ≠Ë®ÄËÆæÁΩÆ > Êõ¥ÊîπÁ≥ªÁªüÂå∫ÂüüËÆæÁΩÆ > ÂãæÈÄâ BetaÁâàÔºöUTF-8„ÄÇ‰∏çÁî®ÈáçÂêØÔºåÂ§öËΩ¨Êç¢Âá†Ê¨°ÔºåÊàêÂäüÂêéÂÜçÊîπÂõûÂéªÔºâ`n'
            '`n2.‰øùÂ≠òÂÆåÊàêÔºåÂú∞ÂùÄÂ∑≤Êã∑Ë¥ùÔºàÂèØÊåâ Ctrl+V Á≤òË¥¥Ôºâ:`n`n' absoluteDir
        )
            MsgBox msg, "CapslockMagic üéä"
        } catch Error as err {
            ToolTip err
            return
        }
    SetTimer () => ToolTip(), -2000
}

    static inputFrom(FileName) {
    ; ÈªòËÆ§ÁõÆÂΩï A_WorkingDir
    ; Áé∞Âú®Â∑≤ÁªèÊääÂÜÖÂÆπÂÜôÂÖ•Êñá‰ª∂‰∫Ü, ÊääÂÆÉ‰ª¨ËØªÂèñÂõûÂÜÖÂ≠ò‰∏≠.
    try
        FileObj := FileOpen(FileName, "r-d")	; ËØªÂèñÊñá‰ª∂ ("r"), ÂÖ±‰∫´Èô§‰∫ÜÂà†Èô§ ("-d") Â§ñÁöÑÊâÄÊúâËÆøÈóÆÊùÉÈôê
        catch Error as err
    {
            MsgBox "Êó†Ê≥ïÊâìÂºÄÊñá‰ª∂ " FileName 
                . "`n`n" Type(err) ": " err.Message
                . "`n`n ËØ∑Ê£ÄÊü•Êñá‰ª∂Ë∑ØÂæÑÊàñÂêçÁß∞ÊòØÂê¶Ê≠£Á°Æ" 
        return
    }
    ; ÈôêÂà∂ËØªÂèñÈ¶ñË°åÁöÑÂ≠óËäÇÈïøÂ∫¶ÔºåÈªòËÆ§‰∏∫ÂÖ®ÊñáÈïøÂ∫¶
    ; CharsToRead := StrLen(textString)
    ; textString := FileObj.Read(CharsToRead)
    textString := FileObj.Read()
    FileObj.Close()
    return textString
    ; MsgBox "The following string was read from the file: " textString
}

    static fileNameAddToday(fileName) {
    name := StrSplit(fileName, ".")
        return name[1] FormatTime(, "yyyyMMdd") "." name[2]
}

}

; Âä®ÁîªÂêàÈõÜ
class Animation {

    static switchTrayIcon() {
    ;  ÂÅúÁî®ËÑöÊú¨Ôºå‰∏∫‰∫ÜÊâìÂ≠ó
    if A_IsSuspended {
        Traytip "‚è∏Ô∏è Â∑≤ÊöÇÂÅú"
        TraySetIcon("bin\img\capslock_pause.ico", 1, 1)
        SetTimer () => Traytip(), -1000
    } else {
        Traytip "üñ§ ÁªßÁª≠ËøêË°å"
        TraySetIcon("bin\img\capslock_run.ico", 1, 1)
        SetTimer () => Traytip(), -1000
    }
}

    static initTrayIcon() {
    TraySetIcon("bin\img\capslock_run.ico", 1, 1)
    ToolTip "üñ§"
    SetTimer () => ToolTip(), -500
}

    ; ÁàÜÁÇ∏Âä®ÁîªÔºåÊåÅÁª≠1.2s
    static bombExploseGif() {
    ToolTip "-----"
    Sleep 100
    ToolTip "*----"
    Sleep 50
    ToolTip "**---"
    Sleep 50
    ToolTip "***--"
    Sleep 50
    ToolTip "****-"
    Sleep 100
    ToolTip " üí£ "
    Sleep 150
    ToolTip "      "
    Sleep 25
    ToolTip " üí£ "
    Sleep 150
    ToolTip "      "
    Sleep 25
    ToolTip " üí• "
    Sleep 500
    ToolTip
    }


}

;---------------------------------------------------------------------o
;                       Â∏∏Áî®ÁΩëÁ´ô
;---------------------------------------------------------------------o

alg4() {
    Run "https://visualgo.net/zh/sorting"
}