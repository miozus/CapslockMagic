SendMode "Input"	; Recommended for new scripts due to its superior speed and reliability.
SetWorkingDir A_ScriptDir	; Ensures a consistent starting directory.

;=====================================================================o
;                    Common Utilities
;---------------------------------------------------------------------o

;=====================================================================o
;                    Activate Or Run
;---------------------------------------------------------------------o
; ç¼“å­˜
global last_winFilter := ""
global shell := ""
;---------------------------------------------------------------------o
ActivateOrRun(to_activate := "", target := "", args := "", workingdir := "", RunAsAdmin := false)
{
    to_activate := Trim(to_activate)
    ; WinShow, %to_activate%
    if (to_activate && winexist(to_activate)) {
        MyGroupActivate(to_activate)
    } else if (target != "")
    {
        ;showtip("not exist, try to start !")
        if (RunAsAdmin)
        {
            if (substr(target, 1, 1) == "\")
                target := substr(target, 2, strlen(target) - 1)
            Run target args, WorkingDir
        } else
        {
            oldTarget := target
            target := WhereIs(target)
            if (target)
            {
                if (SubStr(target, -3) != ".lnk") {
                    ShellRun(target, args, workingdir)
                } else {
                    ; æ£€æŸ¥ lnk æ˜¯å¦æŸå
                    FileGetShortcut target, &OutTarget
                    ; if FileExist(OutTarget)
                    ShellRun(target, args, workingdir)
                }
            } else {
                try
                {
                    if (workingdir && args) {
                        Run oldTarget args, workingdir
                    } else if (workingdir) {
                        Run oldTarget, workingdir
                    } else if (args) {
                        Run oldTarget args
                    } else {
                        Run oldTarget
                    }
                } catch Error
                {
                    tip(Error.Message)
                }
            }
            ; æœ‰çš„åº”ç”¨å¯åŠ¨ï¼Œç«Ÿç„¶ä¸ä¼šç½®é¡¶ï¼Œæ‰€ä»¥éœ€è¦æ¿€æ´»ä¸€ä¸‹
            ; if (to_activate) {
                ; Sleep 1000
                ; WinActivate to_activate
            ; }
        }
    }
}
;---------------------------------------------------------------------o
/*
    é€šè¿‡ shell é™æƒï¼Œä»¥æ™®é€šç”¨æˆ·æƒé™å¯åŠ¨åº”ç”¨
    ShellRun by swagfag 2021/04/24
    Requires AutoHotkey v2.0-a136-feda41f4
    https://www.autohotkey.com/boards/viewtopic.php?t=78190
 */
ShellRunApp(prms*)
{
    shellWindows := ComObject("Shell.Application").Windows
    desktop := shellWindows.FindWindowSW(0, 0, 8, 0, 1)	; SWC_DESKTOP, SWFO_NEEDDISPATCH

    ; Retrieve top-level browser object.
    if tlb := ComObjQuery(desktop,
        "{4C96BE40-915C-11CF-99D3-00AA004AE837}",	; SID_STopLevelBrowser
        "{000214E2-0000-0000-C000-000000000046}")	; IID_IShellBrowser
    {
        ; IShellBrowser.QueryActiveShellView -> IShellView
        if ComCall(15, tlb, "ptr*", &psv := 0) = 0
        {
            ; Define IID_IDispatch.
            IID_IDispatch := Buffer(16)
            NumPut("int64", 0x20400, "int64", 0x46000000000000C0, IID_IDispatch)

            ; IShellView.GetItemObject -> IDispatch (object which implements IShellFolderViewDual)
            ComCall(15, psv, "uint", 0, "ptr", IID_IDispatch, "ptr*", &pdisp := 0)

            ; Get Shell object.
            shell := ComObjFromPtr(pdisp).Application

            ; IShellDispatch2.ShellExecute
            shell.ShellExecute(prms*)

            ObjRelease(psv)
        }
    }
}

;---------------------------------------------------------------------o
; ğŸ“¦ ç¼“å­˜æœºåˆ¶
ShellRun(prms*)
{
    global shell
    try {
        try {
            if (shell) {
                shell.ShellExecute(prms*)
                return
            }
        } catch {
            tip("refresh shell cache")
        }
        ShellRunApp(prms*)
    } catch Error as e {
        MsgBox e.Message
        tip("shell failed")
    }
}
;---------------------------------------------------------------------o
GetProcessName(id := "") {
    if (id == "")
        id := "A"
    else
        id := "ahk_id " . id

    name := WinGetProcessName(id)
    if (name == "ApplicationFrameHost.exe") {
        ;ControlGet hwnd, Hwnd,, Windows.UI.Core.CoreWindow, %id%
        hwnd := ControlGetHwnd("Windows.UI.Core.CoreWindow1", id)
        if hwnd {
            name := WinGetProcessName("ahk_id " hwnd)
        }
    }
    return name
}

HasVal(haystack, needle)
{
    if !(IsObject(haystack)) || (haystack.Length = 0)
        return 0
    for index, value in haystack
        if (value = needle)
            return index
    return 0
}

WinVisible(id)
{
    ;WingetPos x, y, width, height, ahk_id %id%
    title := WinGetTitle("ahk_id " id)
    ;WinGet, state, MinMax, ahk_id %id%
    ;tooltip %x% %y% %width% %height%

    ;sizeTooSmall := width < 300 && height <dict 300 && state != -1 ; -1 is minimized
    empty := !Trim(title)
    ;if (!sizeTooSmall && !empty)
    ;    tooltip %x% %y% %width% %height% "%title%"

    return empty ? 0 : 1
    ;return  sizeTooSmall || empty  ? 0 : 1
}

GetVisibleWindows(winFilter)
{
    ids := []

    id := WinGetList(winFilter, , "Program Manager")
    for this_id in id{
        if (WinVisible(this_id))
        {
            ids.push(this_id)
        }
    }
    if (ids.length == 0)
    {
        pos := Instr(winFilter, "ahk_exe") - StrLen(winFilter) + StrLen("ahk_exe")
        pname := Trim(Substr(winFilter, pos))
        id := WinGetList("ahk_class ApplicationFrameWindow")
        for this_id in id {
            get_name := GetProcessName(this_id)
            if (get_name == pname) {
                ids.push(this_id)
            }
        }
    }
    return ids
}

WhereIs(FileName)
{
    ; æ ¡éªŒè·¯å¾„çº¿ç´¢ï¼Œæ·±å…¥ç³»ç»Ÿå¤šä¸ªç›®å½•ï¼Œç›´åˆ°æ‰¾åˆ°å¯æ‰§è¡Œçš„æ–‡ä»¶åç§°
    ; https://autohotkey.com/board/topic/20807-fileexist-in-path-environment/

    ; Working Folder
    PathName := A_WorkingDir "\"
    if (FileExist(PathName FileName)) {
        Return PathName FileName
    }

    ; absolute path
    if (FileExist(FileName)) {
        Return FileName
    }

    ; Parsing DOS Path variable
    DosPath := EnvGet("Path")
    Loop Parse DosPath, "`;"
    {
        if (A_LoopField == "") {
            Continue
        }
        if (FileExist(A_LoopField "\" FileName)) {
            Return A_LoopField "\" FileName
        }
    }

    ; Looking up Registry
    PathName := RegRead("HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\App Paths\", FileName)
    if (FileExist(PathName)) {
        Return PathName
    }

}

GroupAddId(&GroupName, p1 := "", p2 := "", p3 := "", p4 := "", p5 := "")
{
    static g := 1
    If (GroupName == "") {
        GroupName := "AutoName" g++
    }
    GroupAdd GroupName, p1, p2 p3 p4 p5
}

MyGroupActivate(winFilter)
{

    winFilter := Trim(winFilter)
    if (!winactive(winFilter))
    {
        activateFirstVisible(winFilter)
        return
    }

    ; group æ˜¯çª—å£ç»„å¯¹è±¡, è¿™ä¸ªå¯¹è±¡æ— æ³•è·å–å†…éƒ¨çŠ¶æ€, æ‰€ä»¥ç”¨ win_group_array_form æ¥å‚¨å­˜ä»–çš„çŠ¶æ€
    global win_group
    global win_group_array_form
    global last_winFilter

    ; åˆ¤æ–­æ˜¯å¦è¿›å…¥äº†æ–°çš„çª—å£ç»„
    ; ç¼“å­˜æœºåˆ¶ï¼Œåˆå§‹å€¼ä¸ºç©º
    if (winFilter != last_winFilter)
    {
        last_winFilter := winFilter
        win_group_array_form := []
        win_group := ""	; å»ºç«‹æ–°çš„åˆ†ç»„
    }

    ; å¯¹æ¯”ä¸Šä¸€æ¬¡çš„çŠ¶æ€, è·å–æ–°çš„çª—å£, ç„¶åæŠŠæ–°çª—å£æ·»åŠ åˆ° win_group_array_form çŠ¶æ€å’Œ win_group
    curr_group := GetVisibleWindows(winFilter)
    for val in curr_group
    {
        ; val := curr_group[A_Index]
        if (!HasVal(win_group_array_form, val))
        {
            win_group_array_form.push(val)
            GroupAddId(&win_group, "ahk_id " val)
        }
    }

    GroupActivate win_group, "R"
}

SwitchWindows()
{
    winClass := WinGetClass("A")
    if (winClass == "ApplicationFrameWindow") {
        title := WinGetTitle("A")
        to_check := title " ahk_class ApplicationFrameWindow"
    } else
        to_check := "ahk_exe " GetProcessName()

    MyGroupActivate(to_check)
    return
}

IsBrowser(pname)
{
    Haystack := "chrome.exe,MicrosoftEdge.exe,firefox.exe,360se.exe,opera.exe,iexplore.exe,qqbrowser.exe,sogouexplorer.exe"
    return InStr(Haystack, pname, false)
}

SmartCloseWindow()
{
    if (winactive("ahk_class WorkerW ahk_exe explorer.exe"))
        return

    thisClass := WinGetclass("A")
    name := GetProcessName()
    if IsBrowser(name)
        Send "^w"
    else if WinActive("- Microsoft Visual Studio ahk_exe devenv.exe")
        Send "^{f4}"
    else
    {
        if (thisClass == "ApplicationFrameWindow" || name == "explorer.exe")
            Send "!{f4}"
        else
        ; WM_SYSCOMMAND
            PostMessage 0x112, 0xF060, , , "A"
    }
}

activateFirstVisible(windowSelector)
{
    id := firstVisibleWindow(windowSelector)
    ; WinGet, State, MinMax, ahk_id %id%
    ; if (State = -1)
    ;     WinRestore, ahk_id %id%
    WinActivate "ahk_id " id
}

firstVisibleWindow(windowSelector)
{
    winList := WinGetList(windowSelector)
    for this_id in winList
    {
        ; item := winList A_Index
        title := WinGetTitle(this_id)
        if (Trim(title) != "") {
            return this_id
        }
    }
}

disableOtherHotkey(thisHotkey)
{
    global allHotkeys, EnableChinesePunctuation := false
    for index, value in allHotkeys
    {
        if (value != thisHotkey) {
            hotkey value, "off"
        }
    }
}

enableOtherHotkey(thisHotkey)
{
    global allHotkeys, EnableChinesePunctuation := true
    for index, value in allHotkeys
    {
        if (value != thisHotkey) {
            hotkey value, "on"
        }
    }
}

;=====================================================================o
;                    Utils Macro
;---------------------------------------------------------------------o
; æ¿€æ´»æˆ–è¿è¡Œ, é»˜è®¤åˆå§‹è‹±æ–‡è¾“å…¥æ³•ï¼ˆçœç•¥ä¸å¡«å‚æ•°ï¼›å¦‚éœ€ä¸­æ–‡ï¼Œå†åŠ é€—å·å’Œâ€œä¸­æ–‡â€ï¼Œè¦åŒ…å«åŒå¼•å·ï¼‰
; @Deprecated
activateOrRunOld(title, exe_path, language := "EN") {
    if WinExist(title) {
        WinActivate
    } else {
        Run exe_path
    }
    IME.set(language)	; é»˜è®¤ä»¥å°å†™å¼€å§‹; posh å¤±å»æ§åˆ¶æƒï¼Œè¦ä¸‹ä¸¤ä¸‹æ‰èƒ½ä¿æŒè‹±æ–‡
}
;---------------------------------------------------------------------o
; ä½¿ç”¨ahkå·¥å…·æŸ¥çœ‹æ§ä»¶ï¼Œä»¥ä¾¿è·å¾—æ§åˆ¶æƒé™
openWinSpy() {
    activateOrRunOld("ahk_exe WindowSpy.exe", "bin\plugin\WindowSpy.ahk")
}

; å•å‡»é¼ æ ‡å³é”®ï¼Œä»¥ç¬¬ N ä¸ªæ–¹å¼æ‰“å¼€è¯¥æ–‡ä»¶
openByAppKey(sort := 0) {
    tip(sort--, -1000)
    Send "{AppsKey}"
    Send "{down " sort "}"
    Send "{Enter}"
}
;---------------------------------------------------------------------o
; ç®€å•çš„åå°ç™»é™†æ“ä½œï¼Œè¾“å…¥éªŒè¯ç æ—¶ï¼Œè¾“å…¥æ³•è®¾ä¸ºè‹±æ–‡
loginAdmin() {
    Send "{text}admin"
    Send "{Tab}"
    Send "{text}admin"
    Send "{Tab}"
    IME.set("EN")
}
;---------------------------------------------------------------------o
; å¤åˆ¶æ–‡å­—ç”¨è®°äº‹æœ¬æŸ¥çœ‹
pasteInNotepad()
{
    Send "^c"
    if WinExist("ahk_class Notepad")
    {
        WinActivate
    } else
    {
        Run "Notepad"
        WinWait "ahk_class Notepad"
        WinActivate
    }
    Send "^a^v"
}
;---------------------------------------------------------------------o
copySelectWord() {
    sendEvent "^{Left}^+{Right}"
    Send "^c"
}
;---------------------------------------------------------------------o
getSelectString() {
    Send "^c"
    return A_Clipboard
}

;=====================================================================o
;                     Tooltip Packing
;---------------------------------------------------------------------o
; æ‚¬æµ®æç¤ºçŠ¶æ€ä¿¡æ¯çš„å°è£…
tip(message, duration := -1000)
{
    ToolTip message
    SetTimer closeToolTip, duration
}

closeToolTip()
{
    ToolTip
}

debugModifierKey() {
    ToolTip (
        "> LAlt" GetKeyState("LAlt")
        " LWin" GetKeyState("LWin")
        " LCtrl" GetKeyState("LCtrl")
    )
    SetTimer () => ToolTip(), -1000
}
;---------------------------------------------------------------------o


; ğŸ§ª å¼€å‘å·¥å…·
; ---
; è¾…åŠ©å†™ä»£ç çš„é›†åˆï¼šå–è‰²ï¼Œå–åæ ‡ï¼Œåºåˆ—åŒ–äºŒç»´æ•°ç»„ï¼ˆå‚¨å­˜æ•°æ®åº“ï¼‰
class DevUtils {

   static serilize(nestedObj) {
        inner := "", outer := ""
        for k1, nested in nestedObj.OwnProps() {
            for k2, v2 in nested.OwnProps() {
                inner .= k2 ':"' v2 '", '
            }
            outer .= k1 ': {' Rtrim(inner, ", ") '},`n'
            inner := ""
        }
        outer := "{`n" Rtrim(outer, ",`n") "`n}"
        A_Clipboard := outer
        msgbox outer, "å·²æ‹·è´"
        return outer
    }

    static getPixelSearchCode(d := 5) {
        MouseGetPos & x, &y
        msg := x - d ", " y - d ", " x + d ", " y + d ", " PixelGetColor(x, y)
        A_clipboard := "PixelSearch(&FoundX, &FoundY, " msg ", 3)"
        Tooltip msg " å·²æ‹·è´"
        SetTimer () => ToolTip(), -2000
    }

    static getMousePosCode() {
        MouseGetPos & x, &y
        msg := x " " y
        A_clipboard := 'Click "' msg '"'
        Tooltip msg " å·²æ‹·è´"
        SetTimer () => ToolTip(), -2000
    }
    
}

class Dotfiles {
    
    static HOME_DIR := "C:\Users\" A_UserName
    static DOTS_DIR := "E:\projects\dotfiles"
    static NVIM_DIR := this.HOME_DIR "\AppData\Local\nvim\init.vim"
    static IDEA_DIR := A_AppData "\JetBrains\IntelliJIdea2022.1"
    static POSH_DIR := A_MyDocuments "\WindowsPowerShell\Microsoft.PowerShell_profile.ps1"

    static open() {
        path := A_Programs "\Visual Studio Code\Visual Studio Code.lnk"
        activateOrRun("dotfiles ahk_exe Code.exe", path, this.DOTS_DIR)
    }

    static backup() {

    
        FileCopy this.NVIM_DIR, this.DOTS_DIR "\apps\nvim\.config\nvim\init.vim", true
    
        FileCopy this.IDEA_DIR "\keymaps\macOS For All copy.xml", this.DOTS_DIR "\apps\Idea\keymaps\macOS For All copy.xml", true
    
        ; æœ‰çš„ç‰ˆæœ¬å¯èƒ½æ˜¯è¿™æ ·
        ; FileCopy this.IDEA_DIR "\options\postfixTemplates.xml", this.DOTS_DIR "\apps\Idea\snippets", true
        ; DirCopy this.IDEA_DIR "\jba_config\win.keymaps", this.DOTS_DIR "\apps\Idea\keymaps", true
    
        DirCopy A_AppData "\Code\User\sync", this.DOTS_DIR "\apps\Code\User\sync", true 
        FileCopy this.HOME_DIR "\.ideavimrc", this.DOTS_DIR "\apps\idea\.ideavimrc", true 
        FileCopy this.HOME_DIR "\.ipython\profile_default\ipython_config.py", this.DOTS_DIR "\apps\python\.ipython\profile_default\ipython_config.py", true 
        FileCopy A_AppData "\Code\User\keybindings.json", this.DOTS_DIR "\apps\code\keybindings.json", true
    
        FileCopy this.POSH_DIR, this.DOTS_DIR "\Windows\Microsoft.PowerShell_profile.ps1", true
    
        MsgBox "æœ¬æœºé…ç½®å·²å¤‡ä»½ï¼Œè¯¦æƒ…å¯è§ Git ç‰ˆæœ¬å˜åŒ–`n`n" this.DOTS_DIR
    }
    
    static apply() {
    
        ; this.HOME_DIR := "C:\Users\" A_UserName
        ; this.DOTS_DIR := "E:\projects\dotfiles"
    
        ; this.NVIM_DIR := this.HOME_DIR "\AppData\Local\nvim\init.vim"
        FileCopy this.DOTS_DIR "\apps\nvim\.config\nvim\init.vim", this.NVIM_DIR,  true
    
        ;FileCopy this.DOTS_DIR "\apps\Idea\keymaps\macOS For All Magic.xml", this.IDEA_DIR "\keymaps\macOS For All Magic.xml",  true
    
        ; æœ‰çš„ç‰ˆæœ¬å¯èƒ½æ˜¯è¿™æ ·
        ; FileCopy this.IDEA_DIR "\options\postfixTemplates.xml", this.DOTS_DIR "\apps\Idea\snippets", true
        ; DirCopy this.IDEA_DIR "\jba_config\win.keymaps", this.DOTS_DIR "\apps\Idea\keymaps", true
    
        DirCopy this.DOTS_DIR "\apps\Code\User\sync", A_AppData "\Code\User\sync",  true 
        FileCopy this.DOTS_DIR "\apps\idea\.ideavimrc", this.HOME_DIR "\.ideavimrc",  true 
        ; FileCopy this.DOTS_DIR "\apps\python\.ipython\profile_default\ipython_config.py", this.HOME_DIR "\.ipython\profile_default\ipython_config.py",  true 
        FileCopy  this.DOTS_DIR "\apps\code\keybindings.json", A_AppData "\Code\User\keybindings.json", true
    
        FileCopy this.DOTS_DIR "\Windows\Microsoft.PowerShell_profile.ps1", this.POSH_DIR, true
    
        MsgBox "æœ¬æœºé…ç½®å·²å¤‡ä»½ï¼Œè¯¦æƒ…å¯è§`n`n" this.DOTS_DIR
    }
    
    
}
; æ–‡ä»¶ç®¡ç†å·¥å…·ï¼šè¯»å†™ï¼Œæ–‡ä»¶åæ·»åŠ æ—¥æœŸ
class FileUtils {


    static outputAs(textStr, fileName)
    {
        if (FileName == "" || textStr == "") {
            msgbox "ä¼ è¾“æ•°æ®/æ–‡ä»¶åç§°ä¸ºç©ºï¼Œæ— æ³•å¯¼å‡º"
            return
        }
        try {
            DirCreate ".\dist"
            FileName := ".\dist\" this.fileNameAddToday(fileName)
        }
        catch Error as err {
            ToolTip err.Message
            return
        }
        ; FileName := FileSelect("S16",, "Create a new file:")
        ; connect other script
        absoluteDir := A_WorkingDir LTrim(FileName, ".") 
        A_Clipboard := absoluteDir

        try
            ; è¦æ‰“å¼€æ–‡ä»¶çš„è·¯å¾„, å¦‚æœæœªæŒ‡å®šç»å¯¹è·¯å¾„åˆ™å‡å®šåœ¨ A_WorkingDir ä¸­.
            FileObj := FileOpen(FileName, "w")
        catch Error
        {
            MsgBox "æ— æ³•æ‰“å¼€æ–‡ä»¶ " FileName 
                . "`n`n" Type(err) ": " err.Message
                . "`n`n è¯·æ£€æŸ¥æ–‡ä»¶è·¯å¾„æˆ–åç§°æ˜¯å¦æ­£ç¡®" 
            return
        }
        try {
            ; é€šè¿‡è¿™ç§æ–¹å¼å†™å…¥å†…å®¹åˆ°æ–‡ä»¶æ—¶, è¦ä½¿ç”¨ `r`n è€Œä¸æ˜¯ `n æ¥å¼€å§‹æ–°è¡Œ.
            FileObj.Write(textStr)
            FileObj.Close()
        msg := 
        (
            '1.å½“å‰ç³»ç»Ÿç¼–ç ä¸º ' FileObj.Encoding 
            '`n`næ³¨æ„ï¼šåªæœ‰ UTF-8 æ”¯æŒä¿ç•™ emojiï¼ˆä¿®æ”¹æ–¹æ³•ï¼šç³»ç»Ÿè®¾ç½® > æœç´¢ç®¡ç†è¯­è¨€è®¾ç½® > æ›´æ”¹ç³»ç»ŸåŒºåŸŸè®¾ç½® > å‹¾é€‰ Betaç‰ˆï¼šUTF-8ã€‚ä¸ç”¨é‡å¯ï¼Œå¤šè½¬æ¢å‡ æ¬¡ï¼ŒæˆåŠŸåå†æ”¹å›å»ï¼‰`n'
            '`n2.ä¿å­˜å®Œæˆï¼Œåœ°å€å·²æ‹·è´ï¼ˆå¯æŒ‰ Ctrl+V ç²˜è´´ï¼‰:`n`n' absoluteDir
        )
            MsgBox msg, "CapslockMagic ğŸŠ"
        } catch Error as err {
            ToolTip err
            return
        }
        SetTimer () => ToolTip(), -2000
    }

    static inputFrom(FileName) {
        ; é»˜è®¤ç›®å½• A_WorkingDir
        ; ç°åœ¨å·²ç»æŠŠå†…å®¹å†™å…¥æ–‡ä»¶äº†, æŠŠå®ƒä»¬è¯»å–å›å†…å­˜ä¸­.
        try
            FileObj := FileOpen(FileName, "r-d")	; è¯»å–æ–‡ä»¶ ("r"), å…±äº«é™¤äº†åˆ é™¤ ("-d") å¤–çš„æ‰€æœ‰è®¿é—®æƒé™
        catch Error as err
        {
            MsgBox "æ— æ³•æ‰“å¼€æ–‡ä»¶ " FileName 
                . "`n`n" Type(err) ": " err.Message
                . "`n`n è¯·æ£€æŸ¥æ–‡ä»¶è·¯å¾„æˆ–åç§°æ˜¯å¦æ­£ç¡®" 
            return
        }
        ; é™åˆ¶è¯»å–é¦–è¡Œçš„å­—èŠ‚é•¿åº¦ï¼Œé»˜è®¤ä¸ºå…¨æ–‡é•¿åº¦
        ; CharsToRead := StrLen(textString)
        ; textString := FileObj.Read(CharsToRead)
        textString := FileObj.Read()
        FileObj.Close()
        return textString
        ; MsgBox "The following string was read from the file: " textString
    }

    static fileNameAddToday(fileName) {
        name := StrSplit(fileName, ".")
        return name[1] FormatTime(, "yyyyMMdd") "." name[2]
    }

}

; åŠ¨ç”»åˆé›†
class Animation {

    static switchTrayIcon() {
        ;  åœç”¨è„šæœ¬ï¼Œä¸ºäº†æ‰“å­—
        if A_IsSuspended {
            Traytip "â¸ï¸ å·²æš‚åœ"
            TraySetIcon("bin\img\capslock_pause.ico", 1, 1)
            SetTimer () => Traytip(), -1000
        } else {
            Traytip "ğŸ–¤ ç»§ç»­è¿è¡Œ"
            TraySetIcon("bin\img\capslock_run.ico", 1, 1)
            SetTimer () => Traytip(), -1000
        }
    }

    static initTrayIcon() {
        TraySetIcon("bin\img\capslock_run.ico", 1, 1)
        ToolTip "ğŸ–¤"
        SetTimer () => ToolTip(), -500
    }

    ; çˆ†ç‚¸åŠ¨ç”»ï¼ŒæŒç»­1.2s
    static bombExploseGif() {
        ToolTip "-----"
        Sleep 100
        ToolTip "*----"
        Sleep 50
        ToolTip "**---"
        Sleep 50
        ToolTip "***--"
        Sleep 50
        ToolTip "****-"
        Sleep 100
        ToolTip " ğŸ’£ "
        Sleep 150
        ToolTip "      "
        Sleep 25
        ToolTip " ğŸ’£ "
        Sleep 150
        ToolTip "      "
        Sleep 25
        ToolTip " ğŸ’¥ "
        Sleep 500
        ToolTip
    }
}

; åŠ¨ç”»è®¡æ—¶å™¨
class Timer {
    
    ; å‘¨æœŸ
    static moonRetation(count) {
        i := 1
        Loop count {
            switch Mod(i, 8) {
               case 1 : tooltip "ğŸŒ‘"
               case 2 : tooltip "ğŸŒ’"
               case 3 : tooltip "ğŸŒ“"
               case 4 : tooltip "ğŸŒ”"
               case 5 : tooltip "ğŸŒ•"
               case 6 : tooltip "ğŸŒ–"
               case 7 : tooltip "ğŸŒ—"
               case 0 : tooltip "ğŸŒ˜"
            }
            Sleep 1000
            i++
        }
        tooltip
    }

    ; äºŒè¿›åˆ¶æ²™æ¼
    static hourGlass(count) {
        i := 1
        Loop count {
            if Mod(i, 2) {
                tooltip "â³ " i
            } else {
                tooltip "âŒ› " i
            }
            Sleep 1000
            i++
        }
        tooltip
    }

}

class GC {
    ; æ”¶å°¾é˜¶æ®µï¼ŒæŒ‰ä¸‹å¤–éƒ¨é€»è¾‘é”æ­»çš„é”®ï¼Œè§£é™¤é”å®š(ä¸è¦æŒ‰ CapsLock åœæ­¢)
    static ModifyKey() {
        Send "{Alt}{Ctrl}"
        if GetKeyState("LWin")
        {
            Send "{LWin}"
            Sleep 100
            Send "{Esc}"
        }
        Sleep 100
        ToolTip
        ; ToolTip "  LAlt" GetKeyState("LAlt") " LWin" GetKeyState("LWin") " LCtrl" GetKeyState("LCtrl") 
    }
    
}

;---------------------------------------------------------------------o
;                       å¸¸ç”¨ç½‘ç«™
;---------------------------------------------------------------------o
editArticles() {
    Run "https://www.zhihu.com/creator/manage/creation/all"
    Run "https://mp.csdn.net/mp_blog/manage/article"
    Run "https://www.jianshu.com/writer#/notebooks/51241025/notes/92966389"
    Run "https://www.cnblogs.com/miozus/"
}

alg4() {
    Run "https://visualgo.net/zh/sorting"
}

class Tencent {

    static docs() {
        Run "https://docs.qq.com/desktop/"
    }
}