SendMode "Input"
SetWorkingDir A_ScriptDir
FileEncoding "UTF-8"


#Include FileStream.ahk

class Log {

    ; æ ·å¼æ•ˆæœ
    ; [2022-04-18 19:26:08] æ‰§è¡Œä»»åŠ¡ 294-2: ğŸ˜: project/java/echarts.simple.min.js
    ; [2022-04-18 19:26:23] æ‰§è¡Œä»»åŠ¡ 294-3: âœ…(3) ç³»ç»Ÿæ‰¾ä¸åˆ°æŒ‡å®šçš„è·¯å¾„ã€‚
    
    static logStr := ""
    static tipStr := ""
    static logPath := A_ScriptDir "\dist\log\info" FormatTime(, "yyyyMMdd") ".log"
    ; é‡å¯è„šæœ¬æ—¶é‡ç½®
    static count := 1

    static open() {
        Run this.logPath
    }
    
    ; PowerShell
    static openByPosh() {
        cmd :=  "cat " this.logPath " -Wait -Tail 50"
        Run "wt.exe " cmd
    }
    
    ; progress å†å²è¿›åº¦ï¼Œæ‰‹åŠ¨å¡«å†™ åç»­è¯†åˆ«ç½‘é¡µé¡µç é«˜äº®åŒºåŸŸ + è¯»å–æ—¥å¿—åˆ†æï¼Œè‡ªåŠ¨ç»­å†™æ—¥å¿—
    ; page é¡µç ï¼ˆä»»åŠ¡æ•°ç»„ï¼‰
    ; row è¡Œæ•°ï¼ˆç¬¬Nä¸ªï¼‰
    static init(page, row:="", progress:="") {
        if IsNumber(progress) {
            progress := progress + page - 1
        } 
        this.logStr .= (
            '[' FormatTime(, "yyyy-MM-dd HH:mm:ss") '] ' progress 
        ) 
        if IsNumber(row) {
            ; ç®¡é“æ“ä½œ
            this.tipStr := "P" page "-" row " "
            if row < 10 {
                this.logStr .= '-' row ' |'
            } else {
                this.logStr .= '-' row '|'
            }
        } else {
            this.tipStr := "P" page
            this.logStr .= page " |"
        }
    }
    
    static info(tip, msg:="", default:=false) {
        if msg == "" {
            this.logStr .= tip
        } else {
            this.logStr .= tip " " msg
        }
        tooltip this.tipStr tip
    }

    static save(path:="") {
        if path != "" {
            this.logPath := path
        }
        FileAppend this.logStr '`n', this.logPath, "UTF-8"
        this.logStr := ""
        this.tipStr := ""
    }

    
    static append(args*) {
        loop args.Length {
            this.logStr .= " " args[A_Index]
        }
    }
    
    ; AOP
    static __New() {
        t1 := this.open
        t2 := this.openByPosh
        t3 := this.save
        t4 := this.info
        this.DefineProp('open', {
            call: (self, args*) => this.before(self, t1, args*)
        })
        this.DefineProp('openByPosh', {
            call: (self, args*) => this.before(self, t2, args*)
        })
        this.DefineProp('save', {
            call: (self, args*) => this.before(self, t3, args*)
        })
        this.DefineProp('info', {
            call: (self, args*) => this.around(self, t4, args*)
        })
    }
    
    ; è‡ªå®šä¹‰çš„é¢„å¤„ç†åŠ¨ä½œ
    static before(self, func, args*) {
        if !FileExist(this.logPath) {
            FileStream.appendIfAbsent(this.logPath, "")
        }
        func(self, args*)
    }
    
    static around(self, func, args*) {
        try {
            default := args[3]
            if default {
                this.init(this.count)
            } 
            func(self, args*)
            this.save(this.logPath)
            this.count++
        } catch Error as e {
            func(self, args*)
        }
    }
}
