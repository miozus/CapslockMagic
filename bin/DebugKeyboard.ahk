SendMode "Input"
SetWorkingDir A_ScriptDir

;=====================================================================o
;               Intelj Keymap for debug

global EnableDebugKeyboard := false

; è°ƒè¯•å°é”®ç›˜
; ---
; - è¿›å…¥ï¼š
;   - ä¸€æ¬¡æ€§ï¼šæŒ‰ä½ <4> ä¸æ¾æ‰‹æ­¥å…¥
;   - å¾ªç¯ä½¿ç”¨ï¼šåˆ†å·æ¨¡å¼è¾“å…¥ dbg å¼€å¯
; - é€€å‡ºï¼š
;   - æ˜ç¤ºï¼š<Esc>
;   - é™é»˜ï¼šAIOS å¢åˆ æ”¹ä»£ç æ—¶ï¼Œæ‰“å¼€è®¡ç®—å™¨æ—¶é»˜é»˜é€€å‡º
*4::
{
    global EnableDebugKeyboard
    thisHotkey := A_ThisHotkey
    GC.disableOtherHotkey(thisHotkey)
    EnableDebugKeyboard := true
    ToolTip "ğŸ¦‰"
    keywait "4"
    EnableDebugKeyboard := false
    if (A_PriorKey == "4" && A_TimeSinceThisHotkey < 350)
    {
        Send "{blind}4"
    }
    GC.enableOtherHotkey(thisHotkey)
    ToolTip
}

#Hotif EnableDebugKeyboard
^1:: Debug.stepOverForce()
*1:: Debug.stepOver()
^2:: Debug.stepIntoForce()
*2:: Debug.stepIntoSmart()
^3:: Debug.stepOut()
*3:: Debug.resumeProgram()

*5:: Idea.Jrebel.hotspot()
*6:: Debug.reboot()
*[:: Ide.usage()
*]:: Ide.hierarchy()
*\:: Debug.run2Cursor()

*A:: Debug.exitSilent()
*I:: Debug.exitSilent()
*O:: Debug.exitSilent()
*S:: Debug.exitSilent()
*C:: Debug.exitSilent()

^Tab:: Idea.Jrebel.hotspot()
*Tab:: Launcher.Google.refresh()
^Esc:: Debug.view()
*Esc:: Debug.exit()
*Space:: Debug.evaluate()
#Hotif

; Intelj IDEA è°ƒè¯•åŠ¨ä½œ
; ---
; docs: https://www.cnblogs.com/chiangchou/p/idea-debug.html
class Debug {

    Enable := false

    ; view , when contion is true
    static view() {
        Send "{blind}^+{F8}"
        ToolTip "ğŸ”"
    }

    ; Run to Cursor
    static run2Cursor() {
        Send "{blind}!{F9}"
        ToolTip "I"
    }

    ; Run debug
    static reboot() {
        Send "{blind}^!d"
        ToolTip "ğŸ"
    }

    ; next breakpoint, let me go
    static resumeProgram() {
        Send "{blind}{F9}"
        ToolTip "â–¶ï¸"
    }

    ; step into [ any ] detail [infinity] if entrance exists
    static stepIntoForce() {
        Send "{blind}!+{F7}"
        ToolTip "â†˜ï¸!"
    }

    ; step into [custom] detail [once] if entrance exists
    static stepInto() {
        Send "{blind}{F7}"
        ToolTip "â†˜ï¸"
    }

    ; step into [custom] detail [once] you can choose one
    static stepIntoSmart() {
        Send "{blind}+{F7}"
        ToolTip "â†˜ï¸"
    }

    ;  step over [no] detail like N ext keyword in vim
    static stepOver() {
        Send "{blind}{F8}"
        ToolTip "â¬‡ï¸"
    }

    ; preview last step
    static stepOverForce() {
        Send "{blind}!+{F8}"
        ToolTip "â¬‡ï¸!"
    }

    ; undo step into like over
    static stepOut() {
        Send "{blind}+{F8}"
        ToolTip "â†—ï¸"
    }

    ; open calculate statement
    static evaluateExpression() {
        Send "{blind}!{F8}"
        ToolTip "ğŸ§®"
    }

    static stopDebug() {
        Send "{blind}^{F2}"
        ToolTip "â¹ï¸"
    }

    ; exit and remap as origin key
    static exitSilent() {
        originKey := LTrim(A_ThisHotkey, '*')
        Send "{Blind}" originKey
        this.exit()
    }

    ; exit dbg state
    static exit() {
        global EnableDebugKeyboard
        EnableDebugKeyboard := false
        ToolTip "ğŸ’¨"
        SetTimer () => ToolTip(), -1000
    }


    ; è®¡ç®—è¡¨è¾¾å¼
    static evaluate() {
        global EnableDebugKeyboard
        this.evaluateExpression()
        Sleep 500
        this.exit()
    }

    ; å¯åŠ¨å¼€å…³
    static toggle() {
        global EnableDebugKeyboard
        EnableDebugKeyboard := !EnableDebugKeyboard
        if EnableDebugKeyboard {
            ToolTip "ğŸ¦‰"
        } else {
            ToolTip "ğŸ’¨"
        }
        SetTimer () => ToolTip(), -1000
    }


    ; åˆ‡é¢ï¼šä¸šåŠ¡é€»è¾‘ï¼Œè‡ªåŠ¨åˆ‡æ¢çª—å£
    static around(self, func, args*) {
        try {
            ; æœ€å¸¸ç”¨ï¼šé¦–å…ˆè‡ªåŠ¨åˆ‡æ¢çª—å£
            Launcher.Idea.activate()
            func(self, args*)
        } catch Error as e {
            ToolTip e.Message
            SetTimer () => ToolTip(), -3000
        }
    }

    ; åˆ‡é¢: æ³¨å†Œ
    static __New() {

        fns := [
            this.stepOver,
            this.stepIntoSmart,
            this.resumeProgram
        ]

        for _, fn in fns {
            tmp := fn    ; ç”±äºè¯»å–ä¸€æ¬¡æ€§ï¼Œå¿…é¡»ä¸´æ—¶å‚¨å­˜
            fn_name := LTrim(tmp.Name, 'Debug.')
            this.DefineProp(fn_name, {
                call: (self, args*) => this.around(self, tmp, args*)
            })
        }

    }

}